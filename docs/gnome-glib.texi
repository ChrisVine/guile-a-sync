@node gnome glib,compose,monotonic time,Top

When using the scheme (gnome gtk) bindings of guile-gnome with guile,
in order to provide await semantics on gtk+ callbacks it will normally
be necessary to use the 'await' and 'resume' procedures provided by
the a-sync procedure in the @ref{coroutines,,(a-sync coroutines)}
module directly (calling 'resume' in the gtk+ callback when ready, and
waiting on that callback using 'await').  However when launching
timeouts, file watches or idle events on the glib main loop,
convenience procedures are possible similar to those provided for the
event loop in the @ref{event loop,,(a-sync event-loop)} module.  These
are set out below.

Note that the g-idle-add procedure in guile-gnome is suspect - there
appears to be a garbage collection issue, and if you call the
procedure often enough in a single or multi-threaded program it will
eventually segfault.  g-io-add-watch is also broken in guile-gnome, so
this library uses its own glib-add-watch procedure which is exported
publicly in case it is useful to users.

All the other scheme files provided by this library are by default
compiled by this library to bytecode.  That is not the case with this
module, so as not to create a hard dependency on guile-gnome.

The (a-sync gnome-glib) module provides the following procedures:

@deffn {Scheme Procedure} await-glib-task-in-thread await resume thunk [handler]
This is a convenience procedure which will run 'thunk' in its own
thread, and then post an event to the default glib main loop when
'thunk' has finished.  This procedure calls 'await' in the default
glib main loop and will return the thunk's return value.  It is
intended to be called in a waitable procedure invoked by a-sync.  If
the optional 'handler' argument is provided, then it will be run in
the default glib main loop if 'thunk' throws and its return value will
be the return value of this procedure; otherwise the program will
terminate if an unhandled exception propagates out of 'thunk'.
'handler' should take the same arguments as a guile catch handler
(this is implemented using catch).

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the default glib main loop runs, where the
result of calling 'thunk' will be received.  As mentioned above, the
thunk itself will run in its own thread.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the worker thread starts), which shouldn't
happen unless memory is exhausted or pthread has run out of resources.
Exceptions arising during execution of the task, if not caught by a
handler procedure, will terminate the program.  Exceptions thrown by
the handler procedure will propagate out of g-main-loop-run.

This procedure uses g-idle-add: see the introductory remarks about
that.

Here is an example of the use of await-glib-task-in-thread:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (simple-format #t "1 + 1 is ~A\n"
			 (await-glib-task-in-thread await resume
						    (lambda ()
						      (+ 1 1))))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-task await resume thunk
This is a convenience procedure for use with glib, which will run
'thunk' in the default glib main loop.  This procedure calls 'await'
and will return the thunk's return value.  It is intended to be called
in a waitable procedure invoked by a-sync.  It is the single-threaded
corollary of await-glib-task-in-thread.  This means that (unlike with
await-glib-task-in-thread) while 'thunk' is running other events in
the main loop will not make progress, so blocking calls should not be
made in 'thunk'.

When 'thunk' is executed, this procedure is waiting on 'await', so
'await' and 'resume' cannot be used again in 'thunk' (although 'thunk'
can call a-sync to start another series of asynchronous operations
with a new await-resume pair).  For that reason, await-glib-yield is
usually more convenient for composing asynchronous tasks.  In
retrospect, this procedure offers little over await-glib-yield, apart
from symmetry with await-glib-task-in-thread.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the default glib main loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the task starts), which shouldn't happen
unless memory is exhausted.  Exceptions arising during execution of
the task, if not caught locally, will propagate out of
g-main-loop-run.

This procedure uses g-idle-add: see the introductory remarks about
that.

Here is an example of the use of await-glib-task:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (simple-format #t "1 + 1 is ~A\n"
			 (await-glib-task await resume
					  (lambda ()
					    (+ 1 1))))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-yield await resume
This is a convenience procedure for use with glib, which will
surrender execution to the relevant event loop, so that code in other
a-sync or compose-a-sync blocks can run.  The remainder of the code
after the call to await-yield! in the current a-sync or compose-a-sync
block will execute on the next iteration through the loop.  It is
intended to be called within a waitable procedure invoked by a-sync
(which supplies the 'await' and 'resume' arguments).  It's effect is
similar to calling await-glib-task with a task that does nothing.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the relevant event loop runs: for this purpose
"the relevant event loop" is the event loop given by the 'loop'
argument, or if no 'loop' argument is provided or #f is provided as
the 'loop' argument, then the default event loop.

This procedure should not throw any exceptions unless memory is
exhausted.

This procedure is first available in version 0.12 of this library.

This procedure uses g-idle-add: see the introductory remarks about
that.

Here is an example of the use of await-glib-yield:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (display "In first iteration through event loop\n")
	  (await-glib-yield await resume)
	  (display "In next iteration through event loop\n")))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-generator-in-thread await resume generator proc [handler]
This is a convenience procedure for acting asynchronously on values
yielded by generator procedures.  The 'generator' argument is a
procedure taking one argument, namely a yield argument (see the
documentation on the make-iterator procedure for further details).
This await-glib-generator-in-thread procedure will run 'generator' in
its own worker thread, and whenever 'generator' yields a value will
cause 'proc' to execute in the default glib main loop.

'proc' should be a procedure taking a single argument, namely the
value yielded by the generator.  If the optional 'handler' argument is
provided, then that handler will be run in the default glib main loop
if 'generator' throws; otherwise the program will terminate if an
unhandled exception propagates out of 'generator'.  'handler' should
take the same arguments as a guile catch handler (this is implemented
using catch).

This procedure calls 'await' and will return when the generator has
finished or, if 'handler' is provided, upon the generator throwing an
exception.  This procedure will return #f if the generator completes
normally, or 'guile-a-sync-thread-error if the generator throws an
exception and 'handler' is run (the 'guile-a-sync-thread-error symbol
is reserved to the implementation and should not be yielded by the
generator).

This procedure is intended to be called in a waitable procedure
invoked by a-sync.  It must (like the a-sync procedure) be called in
the same thread as that in which the default glib main loop runs.  As
mentioned above, the generator itself will run in its own thread.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the worker thread starts), which shouldn't
happen unless memory is exhausted or pthread has run out of resources.
Exceptions arising during execution of the generator, if not caught by
a handler procedure, will terminate the program.  Exceptions thrown by
the handler procedure will propagate out of g-main-loop-run.
Exceptions thrown by 'proc', if not caught locally, will also
propagate out of g-main-loop-run.

This procedure is first available in version 0.9 of this library.

This procedure uses g-idle-add: see the introductory remarks about
that.

Here is an example of the use of await-glib-generator-in-thread:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (await-glib-generator-in-thread await resume
					  (lambda (yield)
					    (let loop ((count 0))
					      (when (< count 5)
						(yield (* 2 count))
						(loop (1+ count)))))
					  (lambda (val)
					    (display val)
					    (newline)))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-generator await resume generator proc
This is a convenience procedure for acting asynchronously on values
yielded by generator procedures.  The 'generator' argument is a
procedure taking one argument, namely a yield argument (see the
documentation on the make-iterator procedure for further details).
This await-glib-generator procedure will run 'generator', and whenever
'generator' yields a value will cause 'proc' to execute in the default
glib main loop - each time 'proc' runs it will do so as a separate
event in the main loop and so be multi-plexed with other events.
'proc' should be a procedure taking a single argument, namely the
value yielded by the generator.

This procedure is intended to be called in a waitable procedure
invoked by a-sync.  It is the single-threaded corollary of
await-glib-generator-in-thread.  This means that (unlike with
await-glib-generator-in-thread) while 'generator' is running other
events in the main loop will not make progress, so blocking calls
(other than to the yield procedure) should not be made in
'generator'.

When 'proc' executes, this procedure will have released 'await' and
'resume', so they may be used by 'proc' to initiate other asynchronous
operations sequentially.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the event loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the task starts), which shouldn't happen
unless memory is exhausted.  Exceptions arising during execution of
the generator, if not caught locally, will propagate out of
await-glib-generator.  Exceptions thrown by 'proc', if not caught
locally, will propagate out of g-main-loop-run.

This procedure is first available in version 0.9 of this library.

This procedure uses g-idle-add: see the introductory remarks about
that.

Here is an example of the use of await-glib-generator:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (await-glib-generator await resume
				(lambda (yield)
				  (let loop ((count 0))
				    (when (< count 5)
				      (yield (* 2 count))
				      (loop (1+ count)))))
				(lambda (val)
				  (display val)
				  (newline)))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-timeout await resume msecs thunk
This is a convenience procedure for use with a glib main loop, which
will run 'thunk' in the default glib main loop when the timeout
expires.  This procedure calls 'await' and will return the thunk's
return value.  It is intended to be called in a waitable procedure
invoked by a-sync.  The timeout is single shot only - as soon as
'thunk' has run once and completed, the timeout will be removed from
the event loop.

In practice, calling await-glib-sleep may often be more convenient for
composing asynchronous code than using this procedure.  That is
because, when 'thunk' is executed, this procedure is waiting on
'await', so 'await' and 'resume' cannot be used again in 'thunk'
(although 'thunk' can call a-sync to start another series of
asynchronous operations with a new await-resume pair).  In retrospect,
this procedure offers little over await-glib-sleep.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the default glib main loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the first call to 'await' is made), which
shouldn't happen unless memory is exhausted.  Exceptions thrown by
'thunk', if not caught locally, will propagate out of g-main-loop-run.

Here is an example of the use of await-glib-timeout:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (simple-format #t
			 "Timeout ~A\n"
			 (await-glib-timeout await resume
					     100
					     (lambda ()
					       "expired")))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-sleep await resume msecs
This is a convenience procedure for use with a glib main loop, which
will suspend execution of code in the current a-sync or compose-a-sync
block for the duration of 'msecs' milliseconds.  The event loop will
not be blocked by the sleep - instead any other events in the event
loop (including any other a-sync or compose-a-sync blocks) will be
serviced.  It is intended to be called within a waitable procedure
invoked by a-sync (which supplies the 'await' and 'resume' arguments).

Calling this procedure is equivalent to calling await-glib-timeout
with a 'proc' argument comprising a lambda expression that does
nothing.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the default glib main loop runs.

This procedure should not throw any exceptions unless memory is
exhausted.

This procedure is first available in version 0.12 of this library.

Here is an example of the use of await-glib-sleep:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (display "Entering sleep\n")
	  (await-glib-sleep await resume 500)
	  (display "Timeout expired\n")
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} glib-add-watch ioc cond func [context]
This procedure replaces guile-gnome's g-io-add-watch procedure, which
won't compile.  It attaches a watch on a g-io-channel object to the
main context provided, or if none is provided, to the default glib
main context (the main program loop).  It returns a glib ID which can
be passed subsequently to the g-source-remove procedure.  It should be
possible to call this procedure in any thread.

'ioc' is the g-io-condition object to which the watch is to be
attached, and 'cond' is a list of symbols (being 'in, 'out, 'pri,
'hup, 'err and 'nval) comprising the events for which the watch is
established.

'func' is executed when an event occurs, and takes two arguments:
first the g-io-channel object to which the watch has been attached,
and second a g-io-condition object indicating the watch condition
which has been met (note: the interface for g-io-condition objects is
broken in guile-gnome at present).  The watch is ended either by
'func' returning #f, or by applying g-source-remove to the return
value of this procedure.  Otherwise the watch will continue.

See the documentation on the a-sync-glib-read-watch procedure for
some of the difficulties with using g-io-channel watches with
guile-gnome.
@end deffn

@deffn {Scheme Procedure} a-sync-glib-read-watch resume fd proc
This is a convenience procedure for use with a glib main loop, which
will run 'proc' in the default glib main loop whenever the file
descriptor 'fd' is ready for reading, and apply resume (obtained from
a call to a-sync) to the return value of 'proc'.  'proc' should take
two arguments, the first of which will be set by glib to the
g-io-channel object constructed for the watch and the second of which
will be set to the GIOCondition ('in, 'hup or 'err) provided by glib
which caused the watch to activate.  It is intended to be called in a
waitable procedure invoked by a-sync.  The watch is multi-shot - it is
for the user to bring it to an end at the right time by calling
g-source-remove in the waitable procedure on the id tag returned by
this procedure.  Any port for the file descriptor 'fd' is not
referenced for garbage collection purposes - it must remain valid
while the read watch is active.  This procedure is mainly intended as
something from which higher-level asynchronous file operations can be
constructed, such as the await-glib-getline procedure.

File watches in guile-gnome are implemented using a GIOChannel object,
and unfortunately GIOChannel support in guile-gnome is decaying.  The
only procedure that guile-gnome provides to read from a GIOChannel
object is g-io-channel-read-line, which does not work.  One is
therefore usually left with having to read from a guile port (whose
underlying file descriptor is 'fd') using guile's port input
procedures, but this has its own difficulties because either (i) the
port has to be unbuffered (say by using the open-file or fdopen
procedure with the '0' mode option or the R6RS open-file-input-port
procedure with a buffer-mode of none, or by calling setvbuf), or (ii)
'proc' must deal with everything in the port's buffer by calling
drain-input, or by looping on char-ready? before returning.  This is
because otherwise, if the port is buffered, once the port is read from
there may be further characters in the buffer to be dealt with even
though the GIOChannel watch does not trigger because there is nothing
new to make the file descriptor ready for reading.

Because this procedure takes a 'resume' argument derived from the
a-sync procedure, it must (like the a-sync procedure) in practice be
called in the same thread as that in which the default glib main loop
runs.

This procedure should not throw an exception unless memory is
exhausted, or guile-glib throws for some other reason.  If 'proc'
throws, say because of port errors, and the exception is not caught
locally, it will propagate out of g-main-loop-run.

As an example of how to use a-sync-glib-read-watch, here is the
implementation of await-glib-getline:
@example
(define (await-glib-getline await resume port)
  (define chunk-size 128)
  (define text (make-bytevector chunk-size))
  (define text-len 0)
  (define (append-byte! u8)
    (when (= text-len (bytevector-length text))
      (let ((tmp text))
	(set! text (make-bytevector (+ text-len chunk-size)))
	(bytevector-copy! tmp 0 text 0 text-len)))
    (bytevector-u8-set! text text-len u8)
    (set! text-len (1+ text-len)))
  (define (make-outstring)
    (let ((encoding (or (port-encoding port)
			(fluid-ref %default-port-encoding)
			"ISO-8859-1"))
	  (conversion-strategy (port-conversion-strategy port))
	  (out-bv (make-bytevector text-len)))
      (bytevector-copy! text 0 out-bv 0 text-len)
      (iconv:bytevector->string out-bv encoding conversion-strategy)))
  (define id (a-sync-glib-read-watch resume
				     (port->fdes port)
				     (lambda (ioc status)
				       (let next ()
					 (let ((u8
						(catch 'system-error
						  (lambda ()
						    (get-u8 port))
						  (lambda args
						    (if (or (= EAGAIN (system-error-errno args))
							    (and (defined? 'EWOULDBLOCK) 
								 (= EWOULDBLOCK (system-error-errno args))))
							'more
							(apply throw args))))))
					   (cond
					    ((eq? u8 'more)
					     'more)
					    ((eof-object? u8)
					     (if (= text-len 0)
						 u8
						 (make-outstring)))
					    ((= u8 (char->integer #\return))
					     (if (char-ready? port)
						 (next)
						 'more))
					    ((= u8 (char->integer #\newline))
					     (make-outstring))
					    (else
					     (append-byte! u8)
					     (if (char-ready? port)
						 (next)
						 'more))))))))
  (let next ((res (await)))
    (if (eq? res 'more)
	(next (await))
	(begin
	  (g-source-remove id)
	  (release-port-handle port)
	  res))))
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-getline await resume port
This is a convenience procedure for use with a glib main loop, which
will start a read watch on 'port' for a line of input.  It calls
'await' while waiting for input and will return the line of text
received (without the terminating '\n' character).  The event loop
will not be blocked by this procedure even if only individual
characters or part characters are available at any one time (although
if 'port' references a socket, it should be non-blocking for this to
be guaranteed).  It is intended to be called in a waitable procedure
invoked by a-sync, and this procedure is implemented using
a-sync-glib-read-watch.  If an end-of-file object is encountered which
terminates a line of text, a string containing the line of text will
be returned (and from version 0.3, if an end-of-file object is
encountered without any text, the end-of-file object is returned
rather than an empty string).

For the reasons explained in the documentation on
a-sync-glib-read-watch, this procedure only works correctly if the
port passed to the 'port' argument has buffering switched off (say by
using the open-file, fdopen or duplicate-port procedure with the '0'
mode option, or by calling setvbuf).  This makes the procedure less
useful than would otherwise be the case.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the default glib main loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the first call to 'await' is made), which
shouldn't happen unless memory is exhausted.  With versions of this
library before 0.13, any exceptions because of read errors or
conversion errors would propagate out of g-main-loop-run and could not
be caught locally.  Having read or conversion errors interfering with
anything using the main loop in this way was not a good approach, so
from version 0.13 of this library all read or conversion exceptions
will propagate in the first instance out of this procedure so that
they may be caught locally, say by putting a catch expression around
the call to this procedure, and only out of g-main-loop-run if not
caught in that way.

From version 0.6, the bytes comprising the input text will be
converted to their string representation using the encoding of 'port'
if a port encoding has been set, or otherwise using the program's
default port encoding, or if neither has been set using iso-8859-1
(Latin-1).  Exceptions from conversion errors should not arise with
iso-8859-1 encoding, although the string may not necessarily have the
desired meaning for the program concerned if the input encoding is in
fact different.  From version 0.7, this procedure uses the conversion
strategy for 'port' (which defaults at program start-up to
'substitute); version 0.6 instead always used a conversion strategy of
'error if encountering unconvertible characters).

From version 0.6, this procedure may be used with an end-of-line
representation of either a line-feed (\n) or a carriage-return and
line-feed (\r\n) combination, as from version 0.6 any carriage return
byte will be discarded (this did not occur with earlier versions).

Here is an example of the use of await-glib-getline:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
          (display "Enter a line of text at the keyboard\n")
          (simple-format #t
                         "The line was: ~A\n"
                         (await-glib-getline await resume
					     (open-file-input-port "/dev/tty" (file-options) (buffer-mode none))))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-getblock await resume port size
This is a convenience procedure for use with a glib main loop, which
will start a read watch on 'port' for a block of data, such as a
binary record, of size 'size'.  It calls 'await' while waiting for
input and will return a pair, normally comprising as its car a
bytevector of length 'size' containing the data, and as its cdr the
number of bytes received (which will be the same as 'size' unless an
end-of-file object was encountered part way through receiving the
data).  The event loop will not be blocked by this procedure even if
only individual bytes are available at any one time (although if
'port' references a socket, it should be non-blocking for this to be
guaranteed).  It is intended to be called in a waitable procedure
invoked by a-sync, and this procedure is implemented using
a-sync-glib-read-watch.

As mentioned above, if an end-of-file object is encountered after
receipt of some but not 'size' bytes, then a bytevector of length
'size' will be returned as car and the actual (lesser) number of bytes
inserted in it as cdr.  If an end-of-file object is encountered
without any bytes of data, a pair with eof-object as car and #f as cdr
will be returned.

For the reasons explained in the documentation on
a-sync-glib-read-watch, this procedure only works correctly if the
port passed to the 'port' argument has buffering switched off (say by
using the open-file, fdopen or duplicate-port procedure with the '0'
mode option, or by calling setvbuf).  This makes the procedure less
useful than would otherwise be the case.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the default glib main loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the first call to 'await' is made), which
shouldn't happen unless memory is exhausted.  With versions of this
library before 0.13, any exceptions because of read errors would
propagate out of g-main-loop-run and could not be caught locally.
Having read errors interfering with anything using the main loop in
this way was not a good approach, so from version 0.13 of this library
all read exceptions will propagate in the first instance out of this
procedure so that they may be caught locally, say by putting a catch
expression around the call to this procedure, and only out of
g-main-loop-run if not caught in that way.

This procedure is first available in version 0.11 of this library.
@end deffn

@deffn {Scheme Procedure} a-sync-glib-write-watch resume fd proc
This is a convenience procedure for use with a glib main loop, which
will run 'proc' in the default glib main loop whenever the file
descriptor 'fd' is ready for writing, and apply resume (obtained from
a call to a-sync) to the return value of 'proc'.  'proc' should take
two arguments, the first of which will be set by glib to the
g-io-channel object constructed for the watch and the second of which
will be set to the GIOCondition ('out or 'err) provided by glib which
caused the watch to activate.  It is intended to be called in a
waitable procedure invoked by a-sync.  The watch is multi-shot - it is
for the user to bring it to an end at the right time by calling
g-source-remove in the waitable procedure on the id tag returned by
this procedure.  Any port for the file descriptor 'fd' is not
referenced for garbage collection purposes - it must remain valid
while the read watch is active.  This procedure is mainly intended as
something from which higher-level asynchronous file operations can be
constructed.

The documentation on the a-sync-glib-read-watch procedure comments
about about the difficulties of using GIOChannel file watches with
buffered ports.  The difficulties are not quite so intense with write
watches, but users are likely to get best results by using unbuffered
output ports (say by using the open-file or fdopen procedure with the
'0' mode option or the R6RS open-file-output-port procedure with a
buffer-mode of none, or by calling setvbuf).

Because this procedure takes a 'resume' argument derived from the
a-sync procedure, it must (like the a-sync procedure) in practice be
called in the same thread as that in which the default glib main loop
runs.

This procedure should not throw an exception unless memory is
exhausted, or guile-glib throws for some other reason.  If 'proc'
throws, say because of port errors, and the exception is not caught
locally, it will propagate out of g-main-loop-run.

As an example of how to use a-sync-glib-write-watch, here is the
implementation of await-glib-put-bytevector:
@example
(define (await-glib-put-bytevector await resume port bv)
  (define index 0)
  (define length (bytevector-length bv))
  (define fd (fileno port))
  (_throw-exception-if-regular-file fd)
  (letrec ((id (a-sync-glib-write-watch resume
					(port->fdes port)
					(lambda (ioc status)
					  (catch #t
					    (lambda ()
					      (set! index (+ index (c-write fd
									    bv
									    index
									    (- length index))))
					      (if (< index length)
						  'more
						  #f))
					    (lambda args
					      (g-source-remove id)
					      (release-port-handle port)
					      (apply throw args)))))))
    (let next ((res (await)))
      (if (eq? res 'more)
	  (next (await))
	  (begin
	    (g-source-remove id)
	    (release-port-handle port))))))
@end example
@end deffn

@deffn {Scheme Procedure} await-glib-put-bytevector await resume port bv
This is a convenience procedure which will start a write watch on
'port' for writing the contents of a bytevector 'bv' to the port.  It
calls 'await' while waiting for output to become available.  The event
loop will not be blocked by this procedure even if only individual
bytes can be written at any one time (although if 'port' references a
socket, it should be non-blocking for this to be guaranteed).  It is
intended to be called in a waitable procedure invoked by a-sync, and
this procedure is implemented using a-sync-glib-write-watch.

For reasons of efficiency, this procedure by-passes the port's output
buffer (if any) and sends the output to the underlying file descriptor
directly.  This means that it is most convenient for use with
unbuffered ports.  However, this procedure can be used with a port
with buffered output, but if that is done and the port has previously
been used for output by a procedure other than c-write or an
await-glib-put* procedure, then it should be flushed before this
procedure is called.  Such flushing might block.

This procedure will throw a 'c-write-error exception if passed a
regular file with a file position pointer: there should be no need to
use this procedure with regular files, because they cannot normally
block on write and are always signalled as ready.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the event loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the first call to 'await' is made), say
because a regular file is passed to this procedure, memory is
exhausted or a write exception is encountered.  With versions of this
library before 0.13, any exceptions because of write errors after the
first write would propagate out of g-main-loop-run and could not be
caught locally.  Having write exceptions (say, because of EPIPE)
interfering with anything using the main loop in this way was not a
good approach, so from version 0.13 of this library all write
exceptions will propagate in the first instance out of this procedure
so that they may be caught locally, say by putting a catch expression
around the call to this procedure.

Here is how EPIPE might be tested for:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (catch 'c-write-error
		 (lambda ()
		   (await-glib-put-bytevector await resume port bv))
		 (lambda (key . args)
		   (if (and (>= (length args) 3)
			    (pair? (cddr args))
			    (pair? (caddr args))
			    (eqv? (caaddr args) EPIPE))
		       (begin
			 ... do something to cater for EPIPE ...)
		       (begin
			 ;; possibly rethrow the exception
			 (apply throw key args)))))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example

This procedure is first available in version 0.11 of this library.
@end deffn

@deffn {Scheme Procedure} await-glib-put-string await resume port text
This is a convenience procedure which will start a write watch on
'port' for writing a string to the port.  It calls 'await' while
waiting for output to become available.  The event loop will not be
blocked by this procedure even if only individual characters or part
characters can be written at any one time (although if 'port'
references a socket, it should be non-blocking for this to be
guaranteed).  It is intended to be called in a waitable procedure
invoked by a-sync, and this procedure is implemented using
await-glib-put-bytevector.

For reasons of efficiency, this procedure by-passes the port's output
buffer (if any) and sends the output to the underlying file descriptor
directly.  This means that it is most convenient for use with
unbuffered ports.  However, this procedure can be used with a port
with buffered output, but if that is done and the port has previously
been used for output by a procedure other than c-write or an
await-glib-put* procedure, then it should be flushed before this
procedure is called.  Such flushing might block.

This procedure will throw a 'c-write-error exception if passed a
regular file with a file position pointer: there should be no need to
use this procedure with regular files, because they cannot normally
block on write and are always signalled as ready.

This procedure must (like the a-sync procedure) be called in the same
thread as that in which the event loop runs.

Exceptions may propagate out of this procedure if they arise while
setting up (that is, before the first call to 'await' is made), say
because a regular file is passed to this procedure, memory is
exhausted, a conversion error arises or a write exception is
encountered.  With versions of this library before 0.13, any
exceptions because of write errors after the first write would
propagate out of g-main-loop-run and could not be caught locally.
Having write exceptions (say, because of EPIPE) interfering with
anything using the main loop in this way was not a good approach, so
from version 0.13 of this library all write exceptions will propagate
in the first instance out of this procedure so that they may be caught
locally, say by putting a catch expression around the call to this
procedure.

Here is how EPIPE might be tested for:
@example
(define main-loop (g-main-loop-new #f #f))
(a-sync (lambda (await resume)
	  (catch 'c-write-error
		 (lambda ()
		   (await-glib-put-string await resume port "test"))
		 (lambda (key . args)
		   (if (and (>= (length args) 3)
			    (pair? (cddr args))
			    (pair? (caddr args))
			    (eqv? (caaddr args) EPIPE))
		       (begin
			 ... do something to cater for EPIPE ...)
		       (begin
			 ;; possibly rethrow the exception
			 (apply throw key args)))))
	  (g-main-loop-quit main-loop)))
(g-main-loop-run main-loop)
@end example

The bytes to be sent will be converted from the passed in string
representation using the encoding of 'port' if a port encoding has
been set, or otherwise using the program's default port encoding, or
if neither has been set using iso-8859-1 (Latin-1).  Exceptions from
conversion errors will propagate out of this procedure when setting up
if conversion fails and a conversion strategy of 'error is in effect.
This procedure uses the conversion strategy for 'port' (which defaults
at program start-up to 'substitute).

If CR-LF line endings are to be written when outputting the string,
the '\r' character (as well as the '\n' character) must be embedded in
the string.

This procedure is first available in version 0.10 of this library.
@end deffn
